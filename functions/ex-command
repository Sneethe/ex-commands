local -l method gmethod=ere
[[ -o rematchpcre ]] && gmethod=pcre

emulate -L zsh
setopt extendedglob

local curcontext=zvm-cmd::: prompt REPLY MATCH MBEGIN MEND
local -a cmd match mbegin mend

# Keep history of excmds
fc -ap "${HISTFILE}.excmd"

zstyle -s :zle:zvm-ex-command prompt prompt || prompt=':'
zstyle -s :zle:zvm-ex-command method method || method=$gmethod

read-from-minibuffer "$prompt" || return 1

cmd=("${(@z)REPLY}")

# TODO: Handle:
# - '%' prefix
# - s/// command
# - ?

# for now, just try dequoting the first argument and running a widget
if [[ -n ${(Mk)widgets:#${(Q)cmd[1]}} ]]; then
	zle ${(Q)cmd} -w "${(Q)cmd[2,-1]}"

elif [[ $REPLY = (#b)s([^[:alnum:]])* ]]; then
	local d=$match[1]
	if [[ $REPLY = (#b)s$d((\\?|\\$d|[^\\$d])#)$d((\\?|\\$d|[^\\$d])#)($d*|) ]]; then
		# TODO: Parse flags, figure out if regexp-replace can take them
		local -aU pcreflags
		local -i global insens
		for flag in ${(s::)match[5]:1}; case $flag in
			g) global=1 ;;
			G) method=glob ;;
			p) method=pcre ;;
			i) insens=1 ;;
			I) insens=0 ;;
			*) zle -M "$0: unknown flag: ${(q-)flag}"
		esac
		# remove \$delim
		local find=${match[1]//\\$d/$d} repl=${match[3]//\\$d/$d}
		case $method in
			ere) setopt norematchpcre ;;
			pcre)
				setopt rematchpcre
				((insens)) && find='(?i)'$find
				;;
		esac
		# Shorthand: \N -> $match[n], & -> $MATCH
		# Use ${match:1}, it correctly removes the extra backslash for \\ and \&
		# If ${match:1} is a digit, then ${match[N]}
		# If ${match:1} is empty, then $MATCH
		repl=${repl//(#m)(\\\\|\\&|&|\\[[:digit:]])/${${${MATCH:1}/(#m)[[:digit:]]/'${match['$MATCH']}'}:-'$MATCH'}}
		# get or save $EXLASTSEARCH
		typeset -g EXLASTSEARCH=${${find:=$EXLASTSEARCH}:-$LASTSEARCH}

		case $global$method in
			0*re) if [[ $RBUFFER =~ $2 ]] && RBUFFER=${RBUFFER[1,MBEGIN-1]}${(e)repl}${RBUFFER[MEND+1,-1]} ;;
			1*re) regexp-replace BUFFER "$find" "$repl" ;;
			0glob) RBUFFER=${RBUFFER/(#b)$~find/${(e)repl}} ;; 
			1glob) BUFFER=${BUFFER//(#b)$~find/${(e)repl}} ;;
			*)
				zle -M "$WIDGET: Unknown method: $method"
				return 1
		esac
	else
		zle -M "$WIDGET: Bad substitution"
		return 1
	fi
else
	zle -M "$WIDGET: Unknown command: ${cmd[1]}"
	return 1
fi
print -sr "$REPLY"

# vim:syntax=zsh
